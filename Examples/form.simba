// Simple form example doing random things.

var
  Form: TLazForm;

// Called when the mouse the mouse is pressed on the form
procedure FormMouseDown(Sender: TObject; Button: ELazMouseButton; Shift: ELazShiftStates; X, Y: Integer);
begin
  WriteLn('FormMouseDown: ', Button, ', ', Shift, ' at ', X, ', ', Y);
  if (ELazShiftStates.Shift in Shift) then
    WriteLn('Shift is pressed too!');
  if (ELazShiftStates.Ctrl in Shift) then
    WriteLn('Control is pressed too!');
end;

// Called when the mouse the mouse is released on the form
procedure FormMouseUp(Sender: TObject; Button: ELazMouseButton; Shift: ELazShiftStates; X, Y: Integer);
begin
  WriteLn('FormMouseUp: ', Button, ', ', Shift, ' at ', X, ', ', Y);
end;

procedure DoButtonClick(Sender: TObject);
begin
  WriteLn('Button clicked: ', TLazButton(Sender).GetCaption());

  if (TLazButton(Sender).GetCaption() = 'Close this form') then
    Form.Close();
end;

procedure DoSelectionChange(Sender: TObject; User: Boolean);
begin
  WriteLn('List selection changed to: ', TLazListBox(Sender).GetSelectedText());
end;

procedure DoTabChange(Sender: TObject);
begin
  WriteLn('Tab changed to tab #', TLazPageControl(Sender).GetActiveTabIndex());
end;

procedure ShowMyForm;
var
  Button: TLazButton;
  List: TLazListBox;
  Lbl: TLazLabel;
  Panel: TLazPanel;
  Img: TLazImage;
  SimbaImg: TImage;
  Tabs: TLazPageControl;
  Tab1, Tab2: TLazTabSheet;
  SpeedButton: TLazSpeedButton;
  LazBmp: TLazBitmap;
  Group: TLazGroupBox;
begin
  Form := TLazForm.Create();
  Form.SetCaption('Example form');
  Form.SetOnMouseDown(@FormMouseDown);
  Form.SetOnMouseUp(@FormMouseUp);
  Form.SetWidth(700);
  Form.SetHeight(700);
  Form.SetPosition(ELazFormPosition.ScreenCenter);
  Form.SetColor(Colors.DARK_GREY);
  Form.SetBorderStyle(ELazFormBorderStyle.Single); // Do not allow resizing

  SimbaImg := TImage.CreateFromTarget([0,0,400,400]);

  Group := TLazGroupBox.Create(Form);
  Group.SetParent(Form);
  Group.SetBounds(400,200,200,200);
  Group.SetAutoSize(True);
  Group.SetCaption('Target image');
  Group.GetFont().SetSize(15);

  Img := TLazImage.Create(Group);
  Img.SetParent(Group);
  Img.SetBounds(400,200,200,200);
  Img.SetStretch(True);
  Img.GetPicture().SetBitmap(SimbaImg.ToLazBitmap());
  Img.GetBorderSpacing.SetAround(10);

  SimbaImg.Free();

  Button := TLazButton.Create(Form);
  Button.SetParent(Form);
  Button.SetAutoSize(True);
  Button.SetLeft(300);
  Button.SetTop(50);
  Button.SetShowHint(True);
  Button.SetHint('Mouse over hint');
  Button.SetCaption('This is a button');
  Button.GetFont().SetSize(12);
  Button.SetOnClick(@DoButtonClick);

  Button := TLazButton.Create(Form);
  Button.SetParent(Form);
  Button.SetAutoSize(True);
  Button.SetLeft(300);
  Button.SetTop(100);
  Button.SetCaption('This is a bigger button');
  Button.GetFont().SetSize(18);
  Button.SetOnClick(@DoButtonClick);

  Button := TLazButton.Create(Form);
  Button.SetParent(Form);
  Button.SetAutoSize(True);
  Button.SetLeft(25);
  Button.SetTop(300);
  Button.SetCaption('Close this form');
  Button.GetFont().SetSize(12);
  Button.SetOnClick(@DoButtonClick);

  // Create a panel add a label and listbox in it, which are auto positioned&sized with SetAlign
  Panel := TLazPanel.Create(Form);
  Panel.SetParent(Form);
  Panel.SetBounds(25,25, 250,250);
  Panel.SetBevelOuter(ELazPanelBevel.None);

  Lbl := TLazLabel.Create(Panel);
  Lbl.SetParent(Panel);
  Lbl.SetCaption('List box:');
  Lbl.SetAlign(ELazAlign.Top);

  List := TLazListBox.Create(Panel);
  List.SetParent(Panel);
  List.SetAlign(ELazAlign.Client);
  List.GetItems().Add('Item 1');
  List.GetItems().Add('Item 2');
  List.GetItems().Add('Item 3');
  List.SetOnSelectionChange(@DoSelectionChange);

  Tabs := TLazPageControl.Create(Form);
  Tabs.SetParent(Form);
  Tabs.SetTop(450);
  Tabs.SetLeft(50);
  Tabs.SetWidth(400);
  Tabs.SetOnChange(@DoTabChange);
  Tab1 := Tabs.AddTab();
  Tab1.SetCaption('Tab 1');
  Tab2 := Tabs.AddTab();
  Tab2.SetCaption('Tab 2');

  Button := TLazButton.Create(Tab1);
  Button.SetParent(Tab1);
  Button.SetAutoSize(True);
  Button.SetCaption('This button is on tab 1!');
  Button.SetCursor(ELazCursor.SIZE);

  Button := TLazButton.Create(Tab2);
  Button.SetParent(Tab2);
  Button.SetAutoSize(True);
  Button.SetCaption('This button is on tab 2!');

  LazBmp := TLazBitmap.Create();
  LazBmp.SetWidth(25);
  LazBmp.SetHeight(25);
  with LazBmp.GetCanvas() do
  begin
    GetPen().SetColor(Colors.RED);
    GetBrush().SetColor(Colors.WHITE);
    Rectangle(0,0,25,25);
  end;

  SpeedButton := TLazSpeedButton.Create(Form);
  SpeedButton.SetParent(Form);
  SpeedButton.SetBounds(200,350,150,50);
  SpeedButton.SetCaption('Hello World');
  SpeedButton.SetGlyph(LazBmp);

  Form.ShowModal();

  WriteLn('Form has been closed.');
end;

begin
  RunInMainThread(@ShowMyForm);
end.
