var
  SourceFiles: array of record
    FileName: String;
    Title: String;
  end;

begin
  SourceFiles += ['simba.import_point.pas',        'TPoint'        ];
  SourceFiles += ['simba.import_tpa.pas',          'TPointArray'   ];
  SourceFiles += ['simba.import_atpa.pas',         'T2DPointArray' ];
  SourceFiles += ['simba.import_box.pas',          'TBox'          ];
  SourceFiles += ['simba.import_boxarray.pas',     'TBoxArray'     ];
  SourceFiles += ['simba.import_quad.pas',         'TQuad'         ];
  SourceFiles += ['simba.import_windowhandle.pas', 'TWindowHandle' ];
  SourceFiles += ['simba.import_debugimage.pas',   'Debug Image'   ];
  SourceFiles += ['simba.import_variant.pas',      'Variant'       ];
  SourceFiles += ['simba.import_script.pas',       'Script'        ];
  SourceFiles += ['simba.import_random.pas',       'Random'        ];
  SourceFiles += ['simba.import_class_bitmap.pas', 'TMufasaBitmap' ];
  SourceFiles += ['simba.import_class_dtm.pas',    'TDTM'          ];
  SourceFiles += ['simba.import_input.pas',        'Input'         ];
  SourceFiles += ['simba.import_finder.pas',       'Finder'        ];
  SourceFiles += ['simba.import_target.pas',       'Target'        ];
end;

const
  SOURCE_ROOT_DIR = 'Source/script/imports/';
  SOURCE_IGNORE_DIRS = [
    'backup'
  ];

  OUTPUT_DIR = 'DocGen/output/';
  INPUT_DIR  = 'DocGen/input/';

  CONFIG_FILE = "
from datetime import datetime

project = 'Simba'
author = 'Simba'
copyright = str(datetime.now().year) + ', Simba'
templates_path = ['.']
pygments_style='sphinx'
source_suffix = '.rst'
master_doc = 'index'
highlight_language = 'pascal'
html_favicon = '../images/simba.png'
html_theme = 'sphinx_rtd_theme'
extensions = [
    'sphinx.ext.githubpages',
]
";

  TEMPLATE_FILE = "
{% extends '!layout.html' %}
  {% block footer %} {{ super() }}
  <style>
         .wy-nav-content { min-width: 85%; }
  </style>
{% endblock %}";

  TOC_FILE = "
Welcome to Simba documentation
******************************
.. toctree::

";

function ParseComments(FileName, Name: String): Boolean;
var
  Str, Comments: String;
begin
  for Str in ReadFileContents(FileName).BetweenAll('(*', '*)') do
  begin
    if (Comments <> '') then
      Comments += LineEnding + '----' + LineEnding;
    Comments += Str;
  end;

  Result := Length(Comments) > 0;
  if Result then
  begin
    for Str in ['function', 'procedure', 'operator', 'type', 'var', 'const'] do
      Comments := Comments.Replace(LineEnding + Str + ' ', LineEnding + '::' + LineEnding +  LineEnding + '  ' + Str + ' ');

    Comments := Comments.Replace('Image::', '.. figure::');
    Comments := Comments.Replace('Note::', '.. note::');
    Comments := Comments.Replace('Warning::', '.. warning::');

    WriteFileContents(INPUT_DIR + Name + '.rst', Comments);
    AppendFileContents(INPUT_DIR + 'index.rst', '   ' + Name + LineEnding);
  end;
end;

procedure Build;
var
  I, J: Integer;
  Files: TStringArray;
begin
  Files := FindFiles(SOURCE_ROOT_DIR, True);
  for I := 0 to High(SourceFiles) do
    for J := 0 to High(Files) do
    begin
      if (SourceFiles[I].FileName = ExtractFileName(Files[J])) then
      begin
        ParseComments(Files[J], SourceFiles[I].Title);
        Break;
      end;
    end;
end;

var
  Str: String;
begin
  CreateDirectory(INPUT_DIR);
  CreateDirectory(OUTPUT_DIR);
  DeleteDirectory(INPUT_DIR, True);

  WriteFileContents(INPUT_DIR + 'index.rst', TOC_FILE);
  WriteFileContents(INPUT_DIR + 'conf.py', CONFIG_FILE);
  WriteFileContents(INPUT_DIR + 'layout.html', TEMPLATE_FILE);

  Build();

  WriteLn('Sphinx exit code: ', RunCommand('sphinx-build', ['-q', '-E', INPUT_DIR, OUTPUT_DIR], Str));
  if (Str <> '') then
    WriteLn(Str);

  WriteLn('Link: "' + ExpandFileName(OUTPUT_DIR) + 'index.html"');
end.
