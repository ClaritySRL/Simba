program new;

const
  PAGE_DIALOGS = 0;
  PAGE_IMAGES = 1;
  PAGE_OTHER = 2;

var
  frm: TForm;
  mainMenu: TMainMenu;
  menuHelp, subMenuHelp: TMenuItem;
  btnColorPicker, btnOpenDialog: TButton;
  tabControl: TPageControl;
  tabs: array [0..2] of TTabSheet;
  colorDialog: TColorDialog;
  openDialog: TOpenDialog;
  img, imgText: TImage;
  btnSpin: TSpinEdit;
  statusBar: TStatusBar;
  statusPanelTxt: TStatusPanel;

procedure onClick(sender: TObject); {$IFNDEF CODEINSIGHT} native; {$ENDIF}
var
  i: integer;
begin
  case sender of
    btnColorPicker:
      with colorDialog do
      begin
        init(frm);
        execute();

        client.writeln('Color picked: ' +toStr(getColor()));
        client.writeln('Custom Colors:');

        for i := 0 to (getCustomColors().getCount() - 1) do
          client.writeln(getCustomColors().getStrings(i));

        free();
      end;

    btnOpenDialog:
      with openDialog do
      begin
        init(frm);
        execute();
        client.writeln('File path picked: '+getFileName());
        free();
      end;
  end;
end;

procedure Help_OnClick(sender: TObject);  {$IFNDEF CODEINSIGHT} native; {$ENDIF}
begin
  client.writeln('Click!');
end;

procedure init(); {$IFNDEF CODEINSIGHT} native; {$ENDIF}
var
  i: integer;
  tabCaptions: TStringArray = ['Dialogs', 'Images', 'Other'];
  panelCaptions: TStringArray = ['Hello', 'Bart', 'How Are You', 'Today'];
  tpa: TPointArray;
  bmp: TBitmap;
begin
  frm.init(nil);

  with frm do
  begin
    setWidth(500);
    setHeight(300);
    setPosition(poScreenCenter);
    setCaption('Lape Form - Test');
    setBorderIcons([biSystemMenu, biMinimize]);
  end;


  statusBar.init(frm);

  with (statusBar) do
  begin
    setParent(frm);
    setSimplePanel(false);

    with getPanels() do
    begin
      for i := 0 to 3 do
      begin
        add();
        getItems(i).setWidth(length(panelCaptions[i]) * 8);
        getItems(i).setText(panelCaptions[i]);
      end;
    end;

  end;


  mainMenu.init(frm);

  menuHelp := mainMenu.addMenu('Help');
  subMenuHelp := menuHelp.addMenu('What with');
  subMenuHelp.setOnClick(Help_OnClick);

  bmp.init();
  //bmp.loadFromFile('C:\Simba\help.bmp');

  bmp.loadFromString(16, 16, 16777215, 'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFDF9F5F5E4D6EACAB0E1BB9DDAB393D9B395E6CFBCF7F1ECFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFBF1E9F1D3BBF4E3D4F3E5D9F2E5DAF2E4D9EFE1D3E4CCB9C99D79EAD9CBFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFCF2EAF3D4BBF6E9DDEFDCCDDFBB9ED1A67ED1A57AD9B595EAD6C5EAD8C9BE8C62E7D5C6FFFFFFFFFFFFFFFFFFFEFAF7F6DAC3F7EADFECD8C6D5A87ECFA075CC9D71C5996BC7986CC79971E3CDBAE7D5C4BA895FF4ECE5FFFFFFFFFFFFFCEDE1F9E9DCEEDED0D7AC81D0A077CD9E72C79A6CC4976AC39569BF9066C0946BE5D2BFD7BBA3D6BAA2FFFFFFFFFFFFF8DEC9F6E8DDE3C1A7DCBB9AFBF8F5F5EBE3C5986BC49669C19367BF9066BA8C62C9A685E9DACAB6845AFFFFFFFFFFFFF6D9C1F3E5DAD8AE89F6ECE3FCF8F5E4CBB4E2CCB6FAF6F2BF9066F7F1ECCFB094B88E67ECE0D1AC7445FFFFFFFFFFFFF5D7BFF3E5DAD7AC86F5ECE2FCF8F5E7D3BFF8F3EEF3EAE1BF9066F6F0EACFB094B68A65ECE0D1AB7243FFFFFFFFFFFFF5D9C3F5E7DCDDBB9CE4C8AEFBF8F5FBF8F6F6EEE8C2956DBB8B63B7895FB7895FC5A180E8D8C8B27E53FFFFFFFFFFFFF8E8DCF5E4D6EBD6C7D2A77BD1A881E5D3BFD9BDA1BE8F65B98A63B7895FB28761E0CCBAD3B59CD2B59CFFFFFFFFFFFFFDF8F5EDCDB4F3E6D9E6CEBACFA47BC4986BC29468BE8F64B88A62B58963DAC0AAE3D0BEB07A4DF2EAE3FFFFFFFFFFFFFFFFFFF8ECE3E4C1A3F1E2D5EAD5C3D6B491C59B71C0956DCBA786E2CEBBE4D1C0AF7648E2CFBEFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5E9DFDFBB9CEAD4C2EEE0D2F0E2D6EFE3D5EADCCCD9BDA6B68359E3D0C0FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFAF4F0E9D4C2D3AC8BC79971C19067C2956FD9BEA7F4ECE6FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF');
  subMenuHelp.setBitmap(bmp);

  bmp.free();

  tabControl.init(frm);

  with tabControl do
  begin
    setParent(frm);
    setBounds(0, 0, 400, 254);

    for i := 0 to high(tabs) do
    begin
      tabs[i] := addTabSheet();
      tabs[i].setCaption(tabCaptions[i]);
    end;
  end;

  btnColorPicker.init(frm);

  with btnColorPicker do
  begin
    setParent(tabs[PAGE_DIALOGS]);
    setLeft(10);
    setTop(10);
    setCaption('Color picker');
    setOnClick(onClick);
    setHint('This will open a color picker');
    setShowHint(true);
  end;

  btnOpenDialog.init(frm);

  with btnOpenDialog do
  begin
    setParent(tabs[PAGE_DIALOGS]);
    setLeft(10);
    setTop(40);
    setCaption('File dialog');
    setOnClick(onClick);
  end;

  img.init(frm);

  with img do
  begin
    setParent(tabs[PAGE_IMAGES]);
    setBounds(20, 20, 50, 50);
    getCanvas().rectangle(0, 0, 50, 50);
    getCanvas().setPixel(20, 20, 255);

    setLength(tpa, 2000);

    for i := 0 to high(tpa) do
      tpa[i] := point(randomRange(1, 49), randomRange(1, 49));

    getCanvas().setPixels(tpa, 255);

    setHint('Hello');
    setShowHint(true);
  end;

  imgText.init(frm);

  with imgText do
  begin
    setParent(tabs[PAGE_IMAGES]);
    setBounds(100, 100, 200, 120);
    getCanvas().rectangle(0, 0, 200, 120);
    getCanvas().getFont().setSize(60);
    getCanvas().getFont().setQuality(fqCleartypeNatural);
    getCanvas().textOut(5, 1, 'Hello');
  end;

  btnSpin.init(frm);

  with btnSpin do
  begin
    setParent(tabs[PAGE_OTHER]);
    setBounds(10, 10, 50, 50);
    setValue(25);
    setMinValue(-50);
  end;

  client.writeln(toString(btnSpin.getValue()));
  client.writeln('Showing form...');
  frm.showModal();
end;

procedure free(); {$IFNDEF CODEINSIGHT} native; {$ENDIF}
begin
  if (frm = nil) then
    exit();

  client.writeln('Freeing form...');
  frm.free();
end;

procedure showForm();
begin
  try
    sync(init);
  except
    writeln('ERROR: Failed to initialize form');
  finally
    sync(free);
  end;
end;

begin
  clearDebug();
  showForm();
end.
